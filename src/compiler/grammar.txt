program        → declaration* EOF ;

// DECLARATIONS
declaration    → structDecl | funDecl | varDecl | statement ;
structDecl     → "struct" IDENTIFIER ( "<" IDENTIFIER )?
                 "{" (varDecl | function)* "}" ;
funDecl        → "fn" function ;
varDecl        → type IDENTIFIER ( "=" expression )? ";" ;
type           → "int" | "float" | "double" | "string";

// STATEMENTS
statement      → exprStmt | forStmt | ifStmt | debugStmt | returnStmt | whileStmt | block ;
exprStmt       → expression ";" ;
forStmt        → "for" "(" ( varDecl | exprStmt | ";" )
                           expression? ";"
                           expression? ")" statement ;
ifStmt         → "if" "(" expression ")" block ( "else" block )? ;
debugStmt      → "debug" expression ";" ;
returnStmt     → "return" expression? ";" ;
whileStmt      → "while" "(" expression ")" statement ;
block          → "{" declaration* "}" ;

// EXPRESSIONS
expression     → assignment ;
assignment     → ( call "." )? IDENTIFIER "=" assignment
               | logic_or ;
logic_or       → logic_and ( "||" logic_and )* ;
logic_and      → equality ( "&&" equality )* ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           → factor ( ( "-" | "+" ) factor )* ;
factor         → unary ( ( "/" | "*" ) unary )* ;
unary          → ( "!" | "-" | cast) unary | call ;
call           → primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
primary        → "true" | "false" | "null"
               | INTEGER | FLOAT | DOUBLE
               | STRING | IDENTIFIER
               | "(" expression ")" ;

// UTILITY
function       → returnType IDENTIFIER "(" parameters? ")" block ;
parameters     → IDENTIFIER ( "," IDENTIFIER )* ;
arguments      → expression ( "," expression )* ;
returnType     → type | "void" ;
